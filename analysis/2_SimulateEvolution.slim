species all initialize(){
	//setwd("/projectnb/dispevol/E_Schlatter/kernelSGS/analysis");
	setwd("C:/Users/eschlatter/Dropbox/DispersalSGS/analysis");
	defineConstant("simID", getSeed());
	createDirectory(("../output/" + simID));
	
	// biological parameters
	defineConstant("N_sponge",50000); // population size (when habitat is saturated)
	defineConstant("F_goby",50); // number of offspring per female
	defineConstant("NMAX",0.2); // larval navigation distance
	defineConstant("MATECHOICE_DIST",0.15); // mate choice radius = 10m in reality; multiply by 10 because I'm using 1/10 the actual population size
	
	initializeSLiMModelType("nonWF");
	
	// larvae looking for sponges
	initializeInteractionType(1,"xy",maxDistance=NMAX);
	i1.setInteractionFunction("l",1.0);
	
	// females looking for mates
	initializeInteractionType(2,"xy",maxDistance=MATECHOICE_DIST,sexSegregation='FM');
	i2.setInteractionFunction("l",1.0);
	
	//sponges checking for settlers
	initializeInteractionType(3,"xy",maxDistance=0);
}

species sponge initialize() {
	initializeSpecies(avatar = 'S', color='orange');
	initializeSLiMOptions(dimensionality="xy");
}

species goby initialize() {
	initializeSpecies(avatar='G',color='blue');
	initializeSLiMOptions(dimensionality="xy");
	initializeTreeSeq(simplificationInterval=5); // simplify the tree sequence every 5 ticks
	
	initializeMutationType("m1", 0.5, "f", 0.0); //neutral
	m1.convertToSubstitution = T;
	initializeGenomicElementType("g1", m1, 1.0);
	initializeGenomicElement(g1, 0, 99999); // for right now, stick with one chromosome, but we'll add this in later
	//initializeMutationRate(1e-7); // per base position per gamete. Lit value is 0.5e-8 per site per GENERATION (not gamete), so divide that by 2x the pop size used to generate the estimate? I'll check the paper again...
	initializeMutationRate(0);
	initializeRecombinationRate(1.5e-8); // per base position per gamete, 1cM/Mbp=1e-8 in SLiM's units. Lit says 1.5cM/Mbp, so set to 1.5e-8.
	initializeSex("A"); // I don't think we need X/Y chromosome level of detail
}

// set up populations and map
ticks all 1 early() {
	sponge.addSubpop("p1", asInteger(N_sponge)); //sponges
	goby.addSubpop("p2",asInteger(round(0.9*N_sponge)));
	
	// map png file generated from Rmd file
	mapImage = Image("../data/map_SLiM.png");
	p1.setSpatialBounds(c(0.0, 0.0, 123.286, 261.714)); // distances in km: from dist_x and dist_y in Rmd file
	map = p1.defineSpatialMap("bbr", "xy", mapImage.floatB,
		valueRange=c(0.0, 1.0), colors=c("black", "white"));
	defineConstant("BBR", map);
	
	p2.setSpatialBounds(c(0.0, 0.0, 123.286, 261.714)); // distances in km: from dist_x and dist_y in Rmd file
	map = p2.defineSpatialMap("bbr", "xy", mapImage.floatR,
		valueRange=c(0.0, 1.0), colors=c("black", "white"));
	
	
	// set spatial positions of sponges (anywhere on reef)
	// code credit: https://github.com/MesserLab/SLiM-Extras
	leftToDraw = p1.individuals;
	
	while (size(leftToDraw) > 0)
	{
		positions = p1.pointUniform(size(leftToDraw));
		onReef = p1.spatialMapValue("bbr", positions) == 0.0;
		onReefCount = sum(onReef);
		
		if (onReefCount > 0)
		{
			onReefPositions = positions[repEach(onReef, 2)];
			leftToDraw[seqLen(onReefCount)].setSpatialPosition(onReefPositions);
			leftToDraw = leftToDraw[seqLen(size(leftToDraw) - onReefCount) + onReefCount];
		}
	}
	
	// place gobies in sponge positions
	positions = p1.sampleIndividuals(size(p2.individuals)).spatialPosition;
	p2.individuals.setSpatialPosition(positions);
	
	defineConstant("start",clock());
	
}

// log file. Wait until the simulation's going, so any global variables we need are defined, etc.
ticks all 3 early(){	
	log = community.createLogFile(("../output/" + simID + "/log_" + simID + ".txt"),logInterval=1000);
	log.addCycle(goby);
	log.addCustomColumn("popsize","popsize;");
	//log.addCustomColumn("nomate","nomate;");
	//log.addCustomColumn("foundsponge","foundsponge;");
	defineGlobal("SO_PATH", "../output/" + simID + "/so_" + simID + "_sponge");
	sponge.outputFull(filePath = SO_PATH);
	}

ticks goby 2: first(){
	i2.evaluate(p2);
	defineGlobal('popsize',size(p2.individuals)); 	//  OUTPUT: total successful recruits
}

// reproduction
species goby reproduction(NULL,'F'){
	mate = i2.drawByStrength(receiver = individual,count = 1); // search for a mate within range
	if (size(mate)){  //produce offspring
		clutchSize = rpois(1,asInteger(F_goby));
		subpop.addCrossed(individual,mate,count=clutchSize);
		//individual.tagL0 = F; // OUTPUT: track how many found mates
	}
//	else individual.tagL0 = T; // OUTPUT: track how many found mates
}

//displacement
ticks goby 2: early() {
	moms = p2.subsetIndividuals(sex="F",minAge=1);
//	defineGlobal("nomate", sum(moms.tagL0)/size(moms));  // OUTPUT: track how many didn't find mates
	larvae = p2.subsetIndividuals(maxAge=0);
	p2.deviatePositions(larvae,"stopping",INF,"e",0.36);
}

//find a sponge
ticks all 2: early(){
	i1.evaluate(c(p2,p1));
	p2.individuals.tag=1; //survives, unless later specified
	larvae = p2.subsetIndividuals(maxAge=0);
	for (larva in larvae){
		settle_site = i1.drawByStrength(receiver = larva,count = 1,exerterSubpop = p1); // search for a sponge within range
		if(size(settle_site)){  // if a sponge was found
			larva.setSpatialPosition(settle_site.spatialPosition); //settle there
			larva.tag=1;
		}
		else larva.tag=0; // larva dies
	}
//	defineGlobal('foundsponge',size(larvae[which(larvae.tag==1)])/size(larvae)); // OUTPUT: track how many found a sponge
	goby.killIndividuals(p2.subsetIndividuals(tag=0)); // remove those who didn't find a sponge
}

// nonoverlapping generations
species goby survival() {
	return(individual.age == 0);
}

//competition
ticks all 2: late(){
	i3.evaluate(c(p1,p2));
	p2.individuals.tag=1; // survives, unless later specified
	for(spot in p1.individuals){
		settlers = i3.nearestInteractingNeighbors(spot,count=size(p2.individuals),exerterSubpop=p2);
		n_sett = size(settlers);
		if(n_sett>1){ //if there's competition
			settlers[sample(seqLen(n_sett),n_sett-1)].tag=0;
		}
	}
	goby.killIndividuals(p2.subsetIndividuals(tag=0)); // remove those who lost competition
}

//ticks all seq(from=20000,to=500000,by=20000) late() {
ticks all seq(from=4,to=8,by=2) late() {
	defineGlobal("TS_PATH", "../output/" + simID + "/ts_" + simID + "_t" + community.tick + ".trees");
	goby.treeSeqOutput(TS_PATH);	
	defineGlobal("SO_PATH", "../output/" + simID + "/so_" + simID + "_t" + community.tick);
	goby.outputFull(filePath = SO_PATH);
	
	catn(simID + ": " + community.tick);
}
